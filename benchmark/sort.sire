val N is 16;
val THRESHOLD is 4;
val LENGTH is 128;
val LEAF_STORAGE is 1000/NUM_CORES;
val DEPTH is NUM_CORES_LOG-1;      % Tree depth
val LEAVES is 1 << DEPTH; % Number of leaves

val ct_NEXT is 0;
val ct_END is 1;
val LEFT is 0;
val RIGHT is 1;

% Partition
func partition(var array[len], val len) is
{ var storeIndex;
  var pivot;
  var pivotValue;
  var hold;
  var i;
  pivot      := len/2;
  pivotValue := array[pivot];
  storeIndex := 0;
  
  % Move pivot to end
  array[pivot] := array[len-1];
  array[len-1] := pivotValue;

  % Reorder list (upto the pivot)
  for i in [0 for len-1] do 
    if array[i] <= pivotValue then 
    { hold := array[i];
      array[i] := array[storeIndex];
      array[storeIndex] := hold;
      storeIndex := storeIndex + 1
    }
    else skip;

  % Move pivot to its final place: swap(storeIndex,pivot)
  hold := array[storeIndex];
  array[storeIndex] := array[len-1];
  array[len-1] := hold;

  return storeIndex
}

% Quicksort
proc quicksort(var array[len], val len) is
{ var pivot;
  if len > 1 then 
  { pivot := partition(array, len);
    quicksort(array[0 for pivot+1], pivot);
    quicksort(array[pivot+1 for len-(pivot+1)], len-(pivot+1))
  }
  else skip
}

proc distribute(chanend parent, chanend left, chanend right) is
{ var type;
  var value;
  var child;
  var more;
  child := 1;
  more := true;
  while more do
  { parent ? type;
    if type = ct_NEXT
    then
    { parent ? value;
      if child
      then
      { left ! ct_NEXT;
        left ! value;
        child := ~child
      }
      else
      { right ! ct_NEXT;
        right ! value;
        child := ~child
      }
    }
    else if type = ct_END
    then
      more := false
    else skip
  };
  left ! ct_END;
  right ! ct_END
}

proc gather(chanend parent, chanend left, chanend right) is
{ var type;
  var more[2];
  var min[2];
  % Initial left
  left ? type;
  if type = ct_NEXT
  then
  { left ? min[LEFT];
    more[LEFT] := true
  }
  else if type = ct_END
  then
    more[LEFT] := false
  else skip;
  % Initial right
  right ? type;
  if type = ct_NEXT
  then
  { right ? min[RIGHT];
    more[RIGHT] := true
  }
  else if type = ct_END
  then
    more[RIGHT] := false
  else skip;
  % Gather
  while more[LEFT] or more[RIGHT] do
  { if more[LEFT] and ((~more[RIGHT]) or (min[LEFT] <= min[RIGHT]))
    then
    { parent ! ct_NEXT;
      parent ! min[LEFT];
      left ? type;
      if type = ct_NEXT
      then
        left ? min[LEFT]
      else if type = ct_END
      then
        more[LEFT] := false
      else skip
    }
    else if more[RIGHT] and ((~more[LEFT]) or (min[RIGHT] <= min[LEFT]))
    then
    { parent ! ct_NEXT;
      parent ! min[RIGHT];
      right ? type;
      if type = ct_NEXT
      then
        right ? min[RIGHT]
      else if type = ct_END
      then
        more[RIGHT] := false
      else skip
    }
    else skip
  };
  parent ! ct_END
}

proc branch(chanend parent, chanend left, chanend right) is
{ distribute(parent, left, right);
  gather(parent, left, right)
}

% Receive a token and send back a 1.
proc leaf(chanend parent) is
{ var data[LEAF_STORAGE];
  var num;
  var i;
  var type;
  var more;
  num := 0; 
  more := true;
  % Receive data
  while more do
  { parent ? type;
    if type = ct_NEXT
    then
    { parent ? data[num];
      num := num + 1
    }
    else if type = ct_END
    then
      more := false
    else skip
  };
  % Sort it locally
  quicksort(data, num);
  % Sent it back
  for i in [0 for num] do
  { parent ! ct_NEXT;
    parent ! data[i]
  };
  parent ! ct_END
}

proc controller(chanend tree) is
{ var i;
  var type;
  var value;
  var running;
  for i in [0 for LENGTH] do
  { tree ! ct_NEXT;
    tree ! LENGTH-i-1
  };
  tree ! ct_END;
  running := true;
  while running do
  { tree ? type;
    if type = ct_NEXT
    then
    { tree ? value;
      printvalln(value)
    }
    else if type = ct_END
    then 
      running := false
    else skip
  }
} 

proc main() is
{ chan c[(2*N)-1];
  var i;
  controller(c[0]) &
  par i in [0 for LEAVES] do 
    leaf(c[(LEAVES-1) + i]) &
  par i in [0 for LEAVES-1] do
    branch(c[i], c[(2*i)+1], c[(2*i)+2])
}$

