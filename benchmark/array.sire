val STORAGE     is 4; % Storage per leaf
val N           is 512; % Number of leaves (storage units)
val LENGTH      is N*STORAGE;
val READ        is 0;
val WRITE       is 1;
val HALT        is 2;

proc sync(chanend c) is
{ var sync;
  c ? sync
}

proc read(chanend c, val index, var value) is
{ c ! READ;
  c ! index;
  c ? value
}

proc write(chanend c, val index, val value) is
{ c ! WRITE;
  c ! index;
  c ! value
}

proc halt(chanend c) is
  c ! HALT

proc leaf(chanend c) is
{ var base;
  var data[STORAGE];
  var query;
  var index;
  var value;
  var running;
  
  % Synchronise
  c ! 0;

  % Read 'base' value configuration message
  c ? value;
  c ? base;
  c ? value;
  
  % Run
  running := true;
  while running do 
  { c ? query;
    if query = READ
    then
    { c ? index;
      c ! data[index-base]
    }
    else if query = WRITE
    then
    { c ? index;
      c ? value;
      data[index-base] := value
    }
    else % HALT
      running := false
  }
}

proc branch(chanend root, chanend left, chanend right) is
{ var depth;
  var base;
  var interval;
  var query;
  var index;
  var value;
  var running;
 
  % Synchronise
  sync(left);
  sync(right);
  root ! 0;
 
  % Configure: depth, base, interval
  root  ? depth;
  root  ? base;
  root  ? interval;
  left  ! depth + 1;
  right ! depth + 1;
  left  ! base;
  right ! base + (interval/2);
  left  ! interval/2;
  right ! interval/2;
  
  % Run
  running := true;
  while running do
  { root ? query;
    if query = READ
    then 
    { root ? index;
      if index < base+(interval/2)
      then
        read(left, index, value)
      else
        read(right, index, value);
      root ! value
    }
    else if query = WRITE
    then
    { root ? index;
      root ? value;
      if index < base+(interval/2)
      then
        write(left, index, value)
      else
        write(right, index, value)
    }
    else % HALT
    { halt(left);
      halt(right);
      running := false
    }
  }
}

proc serve(chanend x, chanend root) is
{ var query;
  var index;
  var value;
  var running;
 
  % Synchronise
  sync(root);
  x ! 0;

  % Configure the structure
  root ! 0;
  root ! 0;
  root ! N*STORAGE;
  
  % Run
  running := true;
  while running do 
  { x ? query;
    if query = READ
    then 
    { x ? index;
      read(root, index, value);
      x ! value
    }
    else if query = WRITE
    then
    { x ? index;
      x ? value;
      write(root, index, value)
    }
    else % HALT
    { halt(root);
      running := false
    }
  }
}

proc array(chanend x) is
{ chan c[(2*N)-1];
  var i;
  server() 
    par i in [0 for N] do 
      leaf(c[(N-1)+i])
  { serve(x, c[0]) ||
    par i in [0 for N-1] do
      branch(c[i], c[(2*i)+1], c[(2*i)+2])
  }
}

proc main() is
{ server(chan x)
    array(x)
  { var value;
    var i;
    var total;
    var t;
    
    sync(x);

    %read(x, 0, value);
    %write(x, 0, 0);

    total := 0;
    for i in [0 for LENGTH] do
    { t := time();
      read(x, i, value);
      total := total + (time()-t)
    };
    printstr("Average read time (cycles): ");
    printvalln(total/LENGTH);
    
    total := 0;
    for i in [0 for LENGTH] do
    { t := time();
      write(x, i, i);
      total := total + (time()-t)
    };
    printstr("Average write time (cycles): ");
    printvalln(total/LENGTH);

    halt(x)
  } 
}

